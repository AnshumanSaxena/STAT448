title1 STAT 448 Midterm 2;
title2 Anshuman Saxena;
title3 05/03/2017;

/*data for exercises 1-4*/
data cancer;
	infile '/home/saxena60/448 (2)/Midterm 2/biomarkers.csv' dlm=',' firstobs=2;
	input P8 P14 P19 P33 P37 P49 P55 P64 P70 P80 grp;
	run;

data cancer; 
	set cancer;
	id=_N_;
run;

/* Excercise 1 */

title4 Excercise 1a;
/* a */
proc princomp data=cancer out=pcout;
	var P8 P14 P19 P33 P37 P49 P55 P64 P70 P80;
	id grp;
	ods select Eigenvalues Eigenvectors ScreePlot;
run;

ods text="Based on the eigenvalues, in order to retain 50% of the total variation, we 
will select four principal components. This is because of two reasons, their respective 
eigenvalues are over 1 and their cumulative variation is over 50%. Looking at the scree 
plots we can see the elbow to be at components 2 and 9. So 1 or 8 components.";

title4 Excercise 1b;
/* b */
ods text="We continue with taking four principal components. The first principal 
component has positive coefficients with P19 and P70 and a negative coefficient with P37. 
The second principal component has positive coefficients with P49, P55 and P64 and 
negative coefficient with P33. The third principal component has positive coefficients 
with P14, P55 and P80 and negative coefficient with P64. The fourth principal compenent 
has positive coefficients with P8 and P14 and negative coefficients with P33 and P49.";

title4 Excercise 1c;
/* c */
proc sort data=pcout;
	by grp;
run;

proc sgplot data=pcout;
	scatter y=prin1 x=prin2;
	by grp;
run;

ods text="Above are the scatter plots for each group with the first two principal 
components. By taking a glance at the two plots, we can see the the first group has values 
mostly towards the bottom half of the scatter plot, whereas the second group has values 
towards the top half of the scatter plot. The ranges for principal component 1 in both the 
graphs are pretty similar; (-3, 2) for group 1 and (-2, 3) for group 2. For principal 
component 2, the range for group 2 is wider than group 1; (-1.1, 2) for group 1 and (-3, 3) 
for group 2.";

/* Excercise 2 */

title4 Excercise 2a;
/* a */
data cancer; 
	set cancer;
	if grp=1 then cancerous=0; else cancerous=1;
run;

proc logistic data=cancer;
	model cancerous=P8 P14 P19 P33 P37 P49 P55 P64 P70 P80;
run;

ods text="Above is a logistic regression model of the classification variable 'grp' on 
attributes of protein biomarkers. The global H0 tests shows all tests to have a p-value <0.05, 
hence we can conclude that there is at least one predictor who's coefficient is 
significantly different from 0. Significant predictors, with p-values <0.05 are P19, P55 and 
P70. The rest are insignificant at the 5% level and should be dropped from the model.";

proc logistic data=cancer;
	model cancerous=P8 P14 P19 P33 P37 P49 P55 P64 P70 P80/selection=stepwise;
	ods select ModelBuildingSummary;
run;

ods text="The best model chosen is logit(cancerous=1)~P19 + P70 + P55.";

title4 Excercise 2b;
/* b */
proc logistic data=cancer;
	model cancerous=P19 P70 P55/lackfit influence;
	ods select ParameterEstimates OddsRatios LackFitChiSq InfluencePlots;
run;

ods text="After a quick glance at the diagnostic plots, we can see that there is a pattern 
that the observations towards the end of each group are more influential than the other 
observations. They are poorly accounted for and are causing instability in the model; 
observation 75 can be considered an extreme point. We do not omit any observations for 
being unduly influential since the CBar doesn't have a value greater than 1.

The Hosmer-Lemeshow Goodness of Fit test is greater than 0.05, meaning we fail to reject 
the H0. Hence, we conclude that there isn't a lack of fit issue and the model is adequate.";

title4 Excercise 2c;
/* c */
proc logistic data=cancer;
	model cancerous=P19 P70 P55;
run;

ods text="Above is a logistic regression model of the classification variable 'grp' on 
the chosen attributes of protein biomarkers through stepwise selection (P19, P70 and P55). 
The global H0 tests shows all tests to have a p-value <0.05, hence we can conclude that 
there is at least one predictor who's coefficient is significantly different from 0. The 
parameter estimates show that all variables are significant. The odds ratio are very close 
to 1 (~0.8), indicating that if one has that protein biomarker, having cancer probability 
increases by ~0.8 - 1.";

title4 Excercise 2d;
/* d */
proc logistic data=cancer;
	model cancerous=P19 P70 P55;
	output predprobs=individual out=cancer_out1;
run;

proc freq data=cancer_out1;
    tables cancerous*_into_/nopercent norow nocol;
run;

proc print data=cancer_out1 (obs=10);
run;

ods text="15 out of 162 observations are misclassified, which 9.26% of the observations.";

/* Excercise 3 */

title4 Excercise 3a;
/* a */
proc discrim data=cancer pool=test crossvalidate manova noclassify;
   class grp;
   var P8 P14 P19 P33 P37 P49 P55 P64 P70 P80;
   priors proportional;
run;

ods text="The Test of Homogeneity of Within Covariance Matrix is significant (<0.05), hence 
we reject H0 and conclude that the two groups do not have the same covariance and quadratic 
discriminant analysis (QDA) needs to be implemented. Based on the MANOVA tests we can 
conclude that there is a linear relationship between 'grp' and the continuous variables. 
The null was rejected by all tests, hence the canonical correlations are not zero.";

title4 Excercise 3b;
/* b */
proc stepdisc data=cancer sle=.05 sls=.05;
   class grp;
   var P8 P14 P19 P33 P37 P49 P55 P64 P70 P80;
run;

ods text="Based on the stepwise selection, the variables we retain are P19, P70, P37 and 
P55.";

proc discrim data=cancer method=normal pool=test crossvalidate manova noclassify;
   class grp;
   var P19 P70 P37 P55;
   priors proportional;
run;

ods text="The Test of Homogeneity of Within Covariance Matrix is significant (<0.05), hence 
we reject H0 and conclude that the two groups do not have the same covariance and quadratic 
discriminant analysis (QDA) needs to be implemented. Based on the MANOVA tests we can 
conclude that there is a linear relationship between 'grp' and the selected variables. 
The null was rejected by all tests, hence the canonical correlations are not zero.

The cross-validation error is 0.099, with 8+8=16 misclassified observations. This is 4 more 
than the full model. The seperation performance is roughly the same as part a).";

title4 Excercise 3c;
/* c */
proc surveyselect data=cancer
		method=srs sampsize=50
		rep=1 seed=123456789 out=test; 
run;
proc sort data=test; 
	by id; 
run;
proc sort data=cancer; 
	by id; 
run;
data traindata; 
	merge cancer(in=l) test(in=ll); by id;
	if ll=1 then delete; 
run;

proc discrim data=traindata pool=no testdata=test testout=tout noclassify;
   class grp;
   var P19 P70 P37 P55;
   testid id; 
   priors proportional;
run;

ods text="The following are results from a quadratic discriminant analysis based on 
training and test set. Among 50 observations assigned to the test set, 6 observations are
misclassified and the total error rate is observed as 0.1169, which is larger than the
cross-validation error rate of 0.099 in Exercise 2. The performance is good.";

title4 Excercise 4;
/* Excercise 4 */
ods text="According to the percentage of misclassified observation, the logistic regression 
was the best method to fit this model. In this logistic regression, 15 of 162 observations 
were misclassified, whereas in the discriminant analysis, 16 of 162 observations were 
misclassified. And the test data misclassified 6 of 50 observations.";

/*data for exercise 5*/
data housing;
	infile '/home/saxena60/448 (2)/Midterm 2/housing.txt' dlm=' ';
	input crim zn indus chas nox rm age dis rad tax ptratio b lstat medv;
run;

/* Excercise 5 */

title4 Excercise 5a;
/* a */
proc genmod data=housing;
	model medv=crim zn indus chas nox rm age dis rad tax ptratio b lstat/ 
	dist=gamma link=log type1 type3;
run;

ods text="From the Type 3 analysis, we can see that the insignificant variables are 
'indus' and 'age'. They have a p-value greater than 0.05. We can conclude that these 
variables are not related to median value.";

title4 Excercise 5b;
/* b */
proc genmod data=housing;
	model medv=crim zn indus chas nox rm dis rad tax ptratio b lstat/ 
	dist=gamma link=log type1 type3;
run;

ods text="We first removed 'age', since it had the largest p-value. The AIC value is 2858.8, 
which is smaller than the previous model. There is one variable with a large p-value, 
'indus', hence we remove it and refit the gamma model again.";

proc genmod data=housing;
	model medv=crim zn chas nox rm dis rad tax ptratio b lstat/ 
	dist=gamma link=log type1 type3;
	output out=gammares pred=pmedv stdreschi=schires stdresdev=sdevres;
run;

ods text="After removing 'indus', the AIC value is 2857.65 and there are no more predictors 
which are considered insignificant (all have p-value < 0.05). Based on Type 3 analysis, 
this should be our final model.";

title4 Excercise 5c;
/* c */
proc sgplot data=gammares;
	scatter y=schires x=pmedv;
run;

proc sgplot data=gammares;
	scatter y=sdevres x=pmedv;
run;

ods text="From the residual plots, we see no pattern and no extreme observations. Hence, 
we assume that our model assumptions are adequate to the data.";

/*data for exercise 6*/

data epi;
	infile '/home/saxena60/448 (2)/Midterm 2/epi.dat' dlm=' ';
	input ID Period1 Period2 Period3 Period4 Treat BL Age;
run;

/* Excercise 6 */

title4 Excercise 6a;
/* a */
proc genmod data=epi;
	class Treat;
    model Period4=Treat BL Age / dist=poisson link=log type1 type3;
    output out=poissonres pred=pperiod stdreschi=schiresid stdresdev=sdevresid;
run;

ods text="Based on the parameter estimates log-linear Poisson model, the variable 'Age' 
is insignificant with a p-value of 0.1017. All other predictor variables are significant. 
The same conclusion holds for the Type 3 analysis. The Type 1 analysis, however, classifies 
'Treat' to be insignificant as well as 'Age'.";

title4 Excercise 6b;
/* b */
proc sgplot data=poissonres;
	scatter y=schiresid x=pperiod;
run;

proc sgplot data=poissonres;
	scatter y=sdevresid x=pperiod;
run;

ods text="By looking at the two plots we notice an obvious pattern: the scatter points 
are highly concentrated between the predicted value range of (0, 20). This tells us that 
the predictor variables don't fit the model well because the predicted values are low but 
the residuals are quite high.";

title;
